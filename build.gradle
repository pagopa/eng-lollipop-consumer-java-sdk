plugins {
    id 'java'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

allprojects {
    version = "1.0.0-SNAPSHOT"
    group = 'it.pagopa'
}

// Settings for allowing reproducible build
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

// Generate MD5 checksum on eng-lollipop-consumer-java-sdk jar file
jar.doLast { task ->
    ant.checksum file: task.archivePath
}

// Print all available plugins
task showPlugins {
    project.plugins.each {
        println it.getClass().name
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    // Settings for allowing reproducible build
    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }

}